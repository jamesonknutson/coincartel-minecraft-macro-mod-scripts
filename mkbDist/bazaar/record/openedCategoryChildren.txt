//! @mkb-import-begin
    // info/index/_matchExpressionNBT
        // Used by getItemName, getItemUUID, getItemSBID to extract a regular expression capture group from NBT Data.
            // Params:
            //      {string} <&haystack>    The string to extract the first capture group from.
            //      {string} <&expression>  The expression to test against <&haystack>, must have at least one capture group.
            //
            // Returns the first capture group's content from the <&expression> match, or an empty string if no match is found.
        function _matchExpressionNBT(&haystack,&expr);
            // File: G:/G_DRIVE_CODE/mkbFunctionExporterNew/mkbSource/info/index.txt:34
            &return_string = "";
            ifmatches(%&haystack%,%&expr%,&capture_group,1);
                &return_string = %&capture_group%;
            endif;
            return(%&return_string%);
        endfunction;
    // info/index/getItemSBID
        // Given the NBT Data of an Item, extracts the Item's SBID from the NBT Data.
            // Params:
            //      {string} <&nbt> The NBT Data to extract the Item SBID from.
            //
            // Returns the Item's SBID, or an empty string if one is not found.
        function getItemSBID(&nbt);
            // File: G:/G_DRIVE_CODE/mkbFunctionExporterNew/mkbSource/info/index.txt:103
            &nbt           = strip(%&nbt%);
            &return_string = _matchExpressionNBT(%&nbt%,"\bid\"?\s*:\"([A-Z_0-9:]+)\"");
            return(%&return_string%);
        endfunction;
    // info/index/getItemName
        // Given the NBT Data of an Item (and optionally it's base item), extracts the Display Name of the Item from the NBT Data.
            // Params:
            //      {string} <&nbt>         The NBT Data to extract the Name from.
            //      {string} [&item="air"]  The Item this NBT Data was collected from (only used in cases where the Item's Name cannot be found).
            //      {boolean} [strip=true]  Whether or not to strip formatting codes from the extracted Name.
            //
            // Returns the Display Name of the Item found from it's NBT Data, with formatting codes stripped if [strip] is true.
        function getItemName(&nbt,&item="air",strip=true);
            // File: G:/G_DRIVE_CODE/mkbFunctionExporterNew/mkbSource/info/index.txt:49
            ifmatches(%&nbt%,".*\bName\s*:\"\s*(.*?)\"",&found_name,1);
                if(%strip%);
                    &found_name = strip(%&found_name%);
                endif;
            elseif(%&item% == "air");
                &found_name = "Air";
            endif;
            return(%&found_name%);
        endfunction;
    // bazaar/record/openedCategoryChildren
        // Gets information about the currently opened Category's Children.
            // Params:
                // None
                //
            // Returns an Array of Tunnel Joined Array Strings in format:
                //    0    {number} #slot - The Slot ID to click on in this GUI's Parent GUI to enter this GUI.
                //    1    {string} &item - The String Item ID of the Item in <#slot> in the Parent GUI.
                //    2    {number} #data - The Numeric Data Value of the Item in <#slot> in the Parent GUI.
                //    3    {string} &name - The Display Name of the Item in <#slot> in the Parent GUI.
                //    4    {string} &sbid - The SkyBlock ID of the Item in <#slot> in the Parent GUI (empty string if not found).
                //    5    {number} #children - Amount of child products this Subcategory holds
        function openedCategoryChildren();
            // File: G:/G_DRIVE_CODE/mkbFunctionExporterNew/mkbSource/bazaar/record.txt:169
            for(record,0,1);
                do;
                    #container_start = 0;
                    #container_end   = %CONTAINERSLOTS% - 37;
                    for(#slot,%#container_start%,%#container_end%);
                        &item_id  = getslotitemnbt(%#slot%,,#item_stack,#item_data,&item_nbt);
                        &item_nbt = strip(%&item_nbt%);
                        ifmatches(%&item_nbt%,"(\d+) product",#children,1);
                            if(!%record%);
                                loaded = true;
                                break;
                            endif;
                            &item_name      = getItemName(%&item_nbt%,%&item_id%);
                            &item_sbid      = getItemSBID(%&item_nbt%);
                            &return_array[] = "%#slot%|%&item_id%|%#item_data%|%&item_name%|%&item_sbid%|%#children%";
                        endif;
                    next;
                until((%record%) || (%loaded%));
            next;
            return(&return_array[]);
        endfunction;
//! @mkb-import-end